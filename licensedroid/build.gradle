apply plugin: 'com.android.library'
apply plugin: "com.jfrog.bintray"
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode project.versionCode
        versionName project.versionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:' + rootProject.junit

    compile "com.android.support:recyclerview-v7:${rootProject.supportLibsVersion}"
    compile 'com.android.support:design:' + rootProject.supportLibsVersion
}



def siteUrl = 'https://github.com/raychenon/licensedroid'      // Homepage URL of the library
def gitUrl = 'https://github.com/raychenon/licensedroid.git'   // Git repository URL
def genDescription = 'A library to generate your Open Source licenses page'
group = 'com.raychenon'
version = project.property('libVersion')

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                name 'com.raychenon:licensedroid'
                description = genDescription
                version version
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'raychenon'
                        name 'Raymond Chenon'
                        email 'raymond.chenon@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

javadocJar.dependsOn javadoc

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {

    user = System.getenv('USER')
    key = System.getenv('APIKEY')

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "com.raychenon:licensedroid"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true

        version {
            name = project.property('bintrayVersion')
            desc = genDescription
            released  = new Date()
            vcsTag = project.property('bintrayVcsTag')
        }
    }
    configurations = ['archives']
}